package vnd.json.api

/**
 * SimpleSorting handles the sorting request according to
 * http://jsonapi.org/format/1.0/#fetching-sorting
 *
 * Limitations:
 * - Only one sort field is supported
 * - No dot-separated fields are supported
 */
class SimpleSorting {

    private static final String PARAM_SORT_NAME = 'sort'
    private static final String FIELD_SEPARATOR = ','
    private static final String DESCENDING_PREFIX = '-'

    /** Internal sorting list */
    private final List<Map> sortList

    /** Internal error */
    private final Error error

    /**
     * Default constructor. Populates the internal sort list or the internal error accordingly.
     *
     * @param params Request parameters
     */
    SimpleSorting(Map params, List<String> allowedFields) {
        sortList = parseParams(params)
        error = validate(sortList, allowedFields)
        if (error) sortList = null
    }

    /**
     * Returns a sorting map useful for most GORM functions
     *
     * @return A sorting map
     */
    Map getMap() {
        sortList ? sortList[0] : [:]
    }

    /**
     * Returns the errors generated by this request
     *
     * @return A JSON API error list
     */
    List<Error> getErrors() {
        error ? [error] : []
    }

    /**
     * Parses the sort parameter from the request parameters.
     *
     * @param params Request parameters
     * @return A list of maps with the structure [[sort:field, order:'asc'], [sort:field, order:'desc'], ...]
     */
    private static List<Map> parseParams(Map params) {
        if (params[PARAM_SORT_NAME]) {
            params[PARAM_SORT_NAME].split(FIELD_SEPARATOR).collect { String field ->
                if (field.startsWith(DESCENDING_PREFIX)) [sort:field.drop(DESCENDING_PREFIX.length()), order:'desc']
                else [sort:field, order:'asc']
            }
        }
    }

    /**
     * Validates the sorting list against the allowed fields and the SimpleSorting limitations.
     *
     * @param sortList The internal sorting list
     * @param allowedFields Allowed sort fields
     * @return The corresponding error if any
     */
    private static Error validate(List<Map> sortList, List<String> allowedFields) {
        if (sortList) {
            if (!allowedFields) {
                new Error(status:400,
                          code:'sort-not-supported',
                          title:'Sorting is not supported for this endpoint',
                          sourceParameter:PARAM_SORT_NAME)
            }
            else if (sortList.size() > 1) {
                new Error(status:400,
                          code:'sort-many-fields',
                          title:'Multiple sort fields are not supported for this endpoint',
                          sourceParameter:PARAM_SORT_NAME)
            }
            else if (!allowedFields.contains(sortList[0].sort)) {
                new Error(status:400,
                          code:'sort-bad-field',
                          title:'The specified sort field is not valid for this resource',
                          sourceParameter:PARAM_SORT_NAME)
            }
        }
    }
}
